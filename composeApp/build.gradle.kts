import com.android.build.gradle.internal.api.BaseVariantOutputImpl
import org.jetbrains.kotlin.gradle.ExperimentalKotlinGradlePluginApi
import org.jetbrains.kotlin.gradle.dsl.JvmTarget

val APP_NAME = "N-Zik"

plugins {
    // Multiplatform
    alias(libs.plugins.kotlin.multiplatform)
    alias(libs.plugins.kotlin.compose)
    alias(libs.plugins.jetbrains.compose)

    // Android
    alias(libs.plugins.android.application)
    alias(libs.plugins.room)


    alias(libs.plugins.kotlin.ksp)
    alias(libs.plugins.kotlin.serialization)
}

repositories {
    google()
    mavenCentral()
    maven { url = uri("https://jitpack.io") }
}

kotlin {
    androidTarget {
        @OptIn(ExperimentalKotlinGradlePluginApi::class)
        compilerOptions {
            jvmTarget.set(JvmTarget.JVM_21)
            freeCompilerArgs.add("-Xcontext-parameters")
        }
    }

    sourceSets {
        all {
            languageSettings {
                optIn("org.jetbrains.compose.resources.ExperimentalResourceApi")
            }
        }

        androidMain.dependencies {
            implementation(libs.media3.session)
            implementation(libs.kotlinx.coroutines.guava)
            implementation(libs.extractor)
            implementation(libs.nanojson)
            implementation(libs.androidx.webkit)
            implementation(libs.ktor.okhttp)
            implementation(projects.discordrpc)

            // Related to built-in game, maybe removed in future?
            implementation(libs.compose.runtime.livedata)
        }
        commonMain.dependencies {
            implementation(compose.runtime)
            implementation(compose.foundation)
            implementation(compose.material3)
            implementation(compose.materialIconsExtended)
            implementation(compose.ui)
            implementation(compose.components.resources)
            implementation(compose.components.uiToolingPreview)

            implementation(projects.innertube) {
                exclude(group = "com.google.protobuf", module = "protobuf-java")
            }
            implementation(projects.oldtube)
            implementation(projects.piped)
            implementation(projects.invidious)

            implementation(projects.metrolist) {
                exclude(group = "com.google.protobuf", module = "protobuf-java")
                exclude(group = "com.github.mostafaalagamy", module = "MetrolistExtractor")
            }

            implementation(libs.room)
            implementation(libs.room.runtime)
            implementation(libs.room.sqlite.bundled)

            implementation(libs.mediaplayer.kmp)

            implementation(libs.navigation.kmp)

            //coil3 mp
            implementation(libs.coil.compose.core)
            implementation(libs.coil.compose)
            implementation(libs.coil.mp)
            implementation(libs.coil.network.okhttp)

            implementation(libs.translator)

        }
    }
}

android {
    dependenciesInfo {
        // Disables dependency metadata when building APKs.
        includeInApk = false
        // Disables dependency metadata when building Android App Bundles.
        includeInBundle = false
    }

    androidComponents {
        beforeVariants(selector().withBuildType("release")) {
            it.enable = false
        }
    }

    buildFeatures {
        buildConfig = true
        compose = true
    }

    compileSdk = 37

    defaultConfig {
        applicationId = "com.nevar.nzik"
        minSdk = 24
        targetSdk = 37
        versionCode = 45
        versionName = "4.4.0"

        /*
                UNIVERSAL VARIABLES
         */
        buildConfigField( "Boolean", "IS_AUTOUPDATE", "true" )
        buildConfigField( "String", "APP_NAME", "\"$APP_NAME\"" )
    }

    splits {
        abi {
            reset()
            isUniversalApk = true
        }
    }

    namespace = "app.n_zik.android"

    buildTypes {
        debug {
            manifestPlaceholders += mapOf("appName" to "$APP_NAME-debug")
            applicationIdSuffix = ".debug"

            buildConfigField( "Boolean", "IS_AUTOUPDATE", "false" )
            signingConfig = signingConfigs.getByName("debug")
        }

        create( "full" ) {
            // App's properties
            versionNameSuffix = "-f"
            // Fallback for modules that don't have a 'full' build type (like :discordrpc)
            matchingFallbacks += listOf("release")
        }

        create( "minified" ) {
            // App's properties
            versionNameSuffix = "-m"
            // Fallback for modules that don't have a 'minified' build type (like :discordrpc)
            matchingFallbacks += listOf("release")

            // Package optimization
            isMinifyEnabled = true
            isShrinkResources = true
            proguardFiles(
                getDefaultProguardFile("proguard-android-optimize.txt"),
                "proguard-rules.pro"
            )
        }

        create( "beta" ) {
            initWith( maybeCreate("full") )
            versionNameSuffix = "-b"
            signingConfig = signingConfigs.getByName("debug")
            // Fallback for modules that don't have a 'beta' build type (like :discordrpc)
            matchingFallbacks += listOf("release")
        }

        /**
         * For convenience only.
         * "Forkers" want to change app name across builds
         * just need to change this variable
         */
        forEach {
            it.manifestPlaceholders.putIfAbsent( "appName", APP_NAME )
        }
    }

    applicationVariants.all {
        outputs.map { it as BaseVariantOutputImpl }
               .forEach { output ->
                   val typeName = buildType.name
                   output.outputFileName = "$APP_NAME-$typeName.apk"
               }
    }

    sourceSets.all {
        kotlin.srcDirs("src/$name/kotlin")
    }

    compileOptions {
        isCoreLibraryDesugaringEnabled = true
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    androidResources {
        generateLocaleConfig = true
    }
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(21))
    }
}

compose.resources {
    publicResClass = true
    generateResClass = always
    packageOfResClass = "rimusic.composeapp.generated.resources"
}

room {
    schemaDirectory("$projectDir/schemas")
}

dependencies {
    implementation(libs.compose.activity)
    implementation(libs.compose.foundation)
    implementation(libs.compose.ui)
    implementation(libs.compose.shimmer)
    implementation(libs.androidx.palette)
    implementation(libs.media3.exoplayer)
    implementation(libs.media3.datasource.okhttp)
    implementation(libs.androidx.appcompat)
    implementation(libs.androidx.appcompat.resources)
    implementation(libs.material3)
    implementation(libs.androidx.constraintlayout)
    implementation(libs.compose.animation)
    implementation(libs.kotlin.csv)
    implementation(libs.monetcompat)
    implementation(libs.androidmaterial)
    implementation(libs.timber)
    implementation(libs.androidx.crypto)
    implementation(libs.math3)
    implementation(libs.toasty)
    implementation(libs.androidyoutubeplayer)
    implementation(libs.androidx.glance.widgets)
    implementation(libs.gson)
    implementation(libs.hypnoticcanvas)
    implementation(libs.hypnoticcanvas.shaders)
    implementation(libs.github.jeziellago.compose.markdown)
    implementation(libs.compose.reorderable)

    implementation(libs.room)
    add("kspAndroid", libs.room.compiler)

    implementation(projects.innertube) { exclude(group = "com.google.protobuf", module = "protobuf-java") }
    implementation(projects.oldtube)
    implementation(projects.kugou)
    implementation(projects.lrclib)
    implementation(projects.piped)

    implementation(projects.metrolist) {
        exclude(group = "com.google.protobuf", module = "protobuf-java")
        exclude(group = "com.github.mostafaalagamy", module = "MetrolistExtractor")
    }

    coreLibraryDesugaring(libs.desugaring.nio)

    testImplementation(libs.bundles.junit5)
    testImplementation(libs.mockk)
    testRuntimeOnly(libs.junit.platform)
    compileOnly(libs.lombok)
    annotationProcessor(libs.lombok)
    implementation(libs.jetbrains.annotations)
    implementation(libs.okhttp3.okhttp)

    // Debug only
    debugImplementation(libs.ui.tooling.preview.android)
}

tasks.withType<Test> {
    useJUnitPlatform()
}

